Attribute VB_Name = "ExtratorModulos"
#If VBA7 Then
    Private Declare PtrSafe Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As LongPtr)
#Else
    Public Declare Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As Long)
#End If

Private Vbcoms As Object
Private Vbcom As Object
Private Function getPath() As String
    'se for access usar currentproject no lugar de activeWorkbook
    getPath = CurrentProject.path
End Function
Private Sub Extrair()
On Error GoTo 1
Set Vbcoms = Application.VBE.ActiveVBProject.VBComponents
    Call VerificaPastaExiste("", True)
    Call VerificaPastaExiste("Formularios")
    Call VerificaPastaExiste("Relatorios")
    Call VerificaPastaExiste("Outros")
    Call VerificaPastaExiste("ArquivosSistema")
    For Each Vbcom In Vbcoms
        'se for access usar currentproject no lugar de activeWorkbook
        Vbcom.Export getPath() & "\modules\" + PathFolder(Vbcom.Name) + "\" & Vbcom.Name
    Next

Debug.Print "SUCESSO!!!"
Dim Comentario As String
If MsgBox("Deseja comitar?", vbYesNo, "Atenção") = vbYes Then
    Comentario = InputBox("Informe um comentário (Optional)", "Commit")
    Call ComandoGit(Comentario)
    'comentario
End If
Exit Sub
1:
MsgBox Err.Description, vbCritical, "Atenção."
End Sub
Private Function VerificaPastaExiste(value As String, Optional module As Boolean) As Boolean
Dim fso As Object
Dim caminho As String
If module Then
    caminho = getPath() & "\modules"
Else
    caminho = getPath() & "\modules\" + value
End If
Set fso = CreateObject("Scripting.FileSystemObject")
If fso.FolderExists(caminho) Then
    Call fso.DeleteFolder(caminho)
    Call fso.createfolder(caminho)
    VerificaPastaExiste = True
Else
    Call fso.createfolder(caminho)
    VerificaPastaExiste = True
End If

End Function
Public Sub ComitarProjeto()
Call Extrair
End Sub
Private Sub ComandoGit(Optional Comentario As String)
    Dim cmm As String
    Call Shell("cmd.exe /S /K" & "perl a.pl c:\temp", vbNormalFocus)
    Call Sleep(1000)
    'alterar para currentproject no lugar de activeworkbook em caso de ms access
    cmm = "cd " & getPath()
    cmm = cmm & " & " & "git add ."
    cmm = cmm & " & " & "git commit -m """ & Comentario & "-" & Format(Now, "dd-mm-yyyy hh_mm_ss") & """"
    cmm = cmm & " & " & "git push"
    cmm = cmm & " & " & CurrentProject.FullName
    cmm = cmm & " & " & "exit"
    Call VBA.SendKeys(cmm)
End Sub
Private Function PathFolder(value As String) As String
    Dim SadSistema As String
    Dim SadDemais As String
    SadSistema = "/ExtratorModulos/m_CreateObject/m_DicionarioDados/m_GeraMetodos/m_GerarCodigo/m_Util/SAD_Documentacao/" _
    & "ArrayFactory/Connect/Contexto/DataDAO/DataLayer/ParameterSql/PreConnection/SAD_Object_Documentacao/ToSheet/"
    
    If InStr(1, SadSistema, "/" & value & "/") > 0 Then
        PathFolder = "ArquivosSistema"
    ElseIf InStr(1, value, "Form_") > 0 Then
        PathFolder = "Formularios"
    ElseIf InStr(1, value, "Report_") > 0 Then
        PathFolder = "Relatorios"
    Else
        PathFolder = "Outros"
    End If
End Function
